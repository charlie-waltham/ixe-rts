$unit_type:"/rts/units/minimaker/minimaker"

:Upgrades
    # 0
    "name" "Improved Range"
    "tier" 1
    "costOre" 300
    "costRedon" 0
    "costGreenar" 0
    "costSpectrite" 0
    "attribs" "range|+|25"
    "description" "+25 range"
    "position" V2(224 330)
    "prerequisite" null
    10 TableN

    # 1
    "name" "Efficient Reloaders"
    "tier" 2
    "costOre" 400
    "costRedon" 100
    "costGreenar" 50
    "costSpectrite" 0
    "attribs" "fire_delay|-|1"
    "description" "-0.03s fire delay"
    "position" V2(176 410)
    "prerequisite" 0
    10 TableN

    # 2
    "name" "Synthetic Anticreeper"
    "tier" 2
    "costOre" 400
    "costRedon" 200
    "costGreenar" 100
    "costSpectrite" 0
    "attribs" "autocammo|+|80"
    "description" "Self-generates 80AC/s"
    "position" V2(272 410)
    "prerequisite" 0
    10 TableN

    # 3
    "name" "Additional Mini Maker"
    "tier" 3
    "costOre" 600
    "costRedon" 400
    "costGreenar" 0
    "costSpectrite" 0
    "attribs" "max_units|+|1"
    "description" "+1 Mini Maker"
    "position" V2(176 490)
    "prerequisite" 1
    10 TableN

    # 4
    "name" "Improved Anticreeper Synthesis"
    "tier" 3
    "costOre" 600
    "costRedon" 300
    "costGreenar" 300
    "costSpectrite" 0
    "attribs" "autocammo|+|120"
    "description" "Generates an additional 120AC/s"
    "position" V2(272 490)
    "prerequisite" 2
    10 TableN

    # 5
    "name" "Overclocked Reloaders"
    "tier" 4
    "costOre" 600
    "costRedon" 400
    "costGreenar" 0
    "costSpectrite" 150
    "attribs" "fire_delay|-|1"
    "description" "-0.03s fire delay"
    "position" V2(176 570)
    "prerequisite" 3
    10 TableN

    # 6
    "name" "Additional Mini Maker"
    "tier" 4
    "costOre" 600
    "costRedon" 400
    "costGreenar" 0
    "costSpectrite" 100
    "attribs" "max_units|+|1"
    "description" "+1 Mini Maker"
    "position" V2(272 570)
    "prerequisite" 4
    10 TableN

    List

:DefaultAttributes
    # @DefaultAttributes >attribs
    # Returns a table of attributes and their default values.

    "max_units" 2 # Required on all factories
    "autocammo" 0
    "range" 54
    "fire_delay" 8
    Table

:ApplyAttributes
    # @ApplyAttributes
    # Apply attributes to all units.
    # _DATA = {"units": list, "attribs": table}

    <_DATA{"units"} >units
    <_DATA{"attribs"} >attribs

    # Singular attribs
    if (TableContainsKey(<attribs "max_units"))
        <attribs{"max_units"} @Set("max_units")
    endif

    # Per-unit attribs
    GetTableKeys(<attribs) >keys
    <keys 0 do
        <keys[i] >attrib
        <units 0 do
            switch
                case (<attrib "autocammo" ==)
                    SetUnitProperty(<units[i] "autocammo" <attribs{<attrib} 30.0 /)
                endcase
                case (<attrib "range" ==)
                    @SetControllerArgOnAllParts(<units[i] "$/weapon_maker/weapon_maker.json" "maker" "fire_range" <attribs{<attrib})
                endcase
                case (<attrib "fire_delay" ==)
                    @SetControllerArgOnAllParts(<units[i] "$/weapon_maker/weapon_maker.json" "maker" "fire_delay" <attribs{<attrib})
                endcase
            endswitch
        loop
    loop

:SetControllerArgOnAllParts
    # @SetControllerArgOnAllParts(<unit <partName <controllerName <arg <value)
    # Helper function for @ApplyAttributes.

    >value >arg >controllerName >partName >unit

    GetChildren(<unit) >parts
    <parts 0 do
        if (GetPartData(<parts[i]{"name"}) <partName ==)
            SetControllerArg(<parts[i] <controllerName <arg <value)
        endif
    loop

:PredictDPS
    # @PredictDPS
    # Return a predicted DPS for the given attribs.
    # <_DATA is a table of attribs like @DefaultAttributes.

    3 >numMakers
    20 >acPerShot

    <_DATA{"fire_delay"} GetUpgradeBoost - 30.0 / >rate
    <acPerShot <rate / <numMakers * >dps

    <dps <_DATA{"max_units"} * @Set("dps")

# IGNORE BELOW

:Once
    @Upgrades          @Set("upgrades")
    @DefaultAttributes @Set("attribs")
    <unit_type         @Set("unit_type")

    RegisterForMsg("ApplyAttributes"      self 1 - Concat "ApplyAttributes")
    RegisterForMsg("PredictDPS"           self 1 - Concat "PredictDPS")
    RegisterForMsg("SetDefaultAttributes" self 1 - Concat "SetDefaultAttributes")

:SetDefaultAttributes
    @DefaultAttributes @Set("attribs")

:Get
    >var
    GetScriptVar(self "/rts/factories/factory.irpl" <var)

:Set
    >var >val
    SetScriptVar(self "/rts/factories/factory.irpl" <var <val)