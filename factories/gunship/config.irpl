$unit_type:"$/gunship/gunship"
$max_units:2

:Upgrades
    # 0
    "name" "Root"
    "tier" 0
    "costOre" 0
    "costRedon" 0
    "costGreenar" 0
    "costSpectrite" 0
    "unitFunction" null
    "onceFunction" null
    "description" "Root node"
    "position" V2(224 250)
    "prerequisite" null
    11 TableN

    # 1
    "name" "Increased Rocket Firerate"
    "tier" 1
    "costOre" 200
    "costRedon" 0
    "costGreenar" 0
    "costSpectrite" 0
    "unitFunction" "IncreasedRocketFirerate"
    "onceFunction" null
    "description" "-0.5s rocket fire delay"
    "position" V2(224 330)
    "prerequisite" 0
    11 TableN

    # 2
    "name" "Efficient Rocket Engines"
    "tier" 2
    "costOre" 200
    "costRedon" 50
    "costGreenar" 200
    "costSpectrite" 0
    "unitFunction" "EffRockets"
    "onceFunction" null
    "description" "-50% rocket energy use"
    "position" V2(176 410)
    "prerequisite" 1
    11 TableN

    # 3
    "name" "Increased Rocket Range"
    "tier" 2
    "costOre" 300
    "costRedon" 150
    "costGreenar" 0
    "costSpectrite" 0
    "unitFunction" "IncreasedRocketRange"
    "onceFunction" null
    "description" "+40 Rocket Range"
    "position" V2(272 410)
    "prerequisite" 1
    11 TableN

    # 4
    "name" "Additional Gunship"
    "tier" 3
    "costOre" 500
    "costRedon" 100
    "costGreenar" 0
    "costSpectrite" 0
    "unitFunction" null
    "onceFunction" "AddGunship"
    "description" "+1 max Gunships"
    "position" V2(176 490)
    "prerequisite" 2
    11 TableN

    # 5
    "name" "Increased Cannon Firerate"
    "tier" 3
    "costOre" 200
    "costRedon" 300
    "costGreenar" 0
    "costSpectrite" 25
    "unitFunction" "IncreasedCannonFirerate"
    "onceFunction" null
    "description" "-0.03s cannon fire delay"
    "position" V2(272 490)
    "prerequisite" 3
    11 TableN

    # 6
    "name" "Sulf-Sufficiency"
    "tier" 4
    "costOre" 400
    "costRedon" 0
    "costGreenar" 500
    "costSpectrite" 100
    "unitFunction" "SelfSufficiency"
    "onceFunction" null
    "description" "-100% energy use (cannon and rocket)"
    "position" V2(176 570)
    "prerequisite" 4
    11 TableN

    # 7
    "name" "Additional Gunship"
    "tier" 4
    "costOre" 750
    "costRedon" 100
    "costGreenar" 0
    "costSpectrite" 200
    "unitFunction" null
    "onceFunction" "AddGunship"
    "description" "+1 max Gunships"
    "position" V2(272 570)
    "prerequisite" 5
    11 TableN

    List

:SelfSufficiency
    >unit
    SetUnitProperty(<unit "autoammo" 3)

:IncreasedRocketRange
    >unit
    GetChildren(<-unit) ->parts
    <-parts 0 do
        if (GetPartData(<-parts[i]){"name"} "$/weapon_rocket/weapon_rocket.json" ==)
            GetScript1Var(<parts[i] "FIRE_RANGE") >range
            # Sometimes it fails to read at unpredictable times
            if (IsNull(<range)) 75 >range endif
            SetScript1Var(<parts[i] "FIRE_RANGE" <range 40 +)
        endif
    loop

:IncreasedRadius
    >unit
    GetChildren(<-unit) ->parts
    <-parts 0 do
        if (GetPartData(<-parts[i]){"name"} "$/weapon_cannon/weapon_cannon.json" ==)
            GetControllerArg2(<parts[i] 0 "shot_damage_distance") >radius
            if (IsNull(<radius)) 8 >radius endif
            SetControllerArg2(<parts[i] 0 "shot_damage_distance" <radius 3 +)
        endif
    loop

:IncreasedRocketFirerate
    >unit
    GetChildren(<-unit) ->parts
    <-parts 0 do
        if (GetPartData(<-parts[i]){"name"} "$/weapon_rocket/weapon_rocket.json" ==)
            GetScript1Var(<parts[i] "FIRE_DELAY") >delay
            if (IsNull(<delay)) 100 >delay endif
            SetScript1Var(<parts[i] "FIRE_DELAY" max(30 <delay 15 -))
        endif
    loop

:IncreasedCannonFirerate
    >unit
    GetChildren(<-unit) ->parts
    <-parts 0 do
        if (GetPartData(<-parts[i]){"name"} "$/weapon_cannon/weapon_cannon.json" ==)
            GetControllerArg2(<parts[i] 0 "fire_delay") >delay
            if (IsNull(<delay)) 10 >delay endif
            SetControllerArg2(<parts[i] 0 "fire_delay" <delay 1 -)
        endif
    loop

:EffRockets
    >unit
    GetChildren(<-unit) ->parts
    <-parts 0 do
        if (GetPartData(<-parts[i]){"name"} "$/weapon_rocket/weapon_rocket.json" ==)
            GetScript1Var(<parts[i] "FIRE_COST") >cost
            if (IsNull(<cost)) 0.5 >cost endif
            SetScript1Var(<parts[i] "FIRE_COST" <cost 2.0 /)
        endif
    loop

:AddGunship
    @Get("max_units") 1 + @Set("max_units")

# IGNORE BELOW

:Once
    @Upgrades >upgrades
    <upgrades @Set("upgrades")
    <unit_type @Set("unit_type")
    <max_units @Set("max_units")
    RegisterForMsg("Upgrade" self 1 - Concat "Upgrade")
    RegisterForMsg("ApplyUpgrades" self 1 - Concat "ApplyUpgrades")

:Upgrade
    # @Upgrade
    # Apply new upgrade to all current units

    <_DATA{"tier"} <maxTier max >maxTier

    <_DATA{"onceFunction"} >onceFunction
    if (<onceFunction IsNull !)
        refcall(<onceFunction)
    endif

    <_DATA{"unitFunction"} >unitFunction
    @Get("units") >units

    <units 0 do
        SetColor(<units[i] @Get("tierColours")[<maxTier])
        if (<unitFunction IsNull !)
            <units[i] refcall(<unitFunction)
        endif
    loop

:ApplyUpgrades
    # @ApplyUpgrades
    # Apply all owned upgrades to a given unit

    <_DATA >unit
    @Get("ownedUpgrades") >ownedUpgrades
    <ownedUpgrades 0 do
        # Messy because I decided to store owned upgrades by index at some point
        <upgrades[<ownedUpgrades[i]]{"unitFunction"} >unitFunction
        if (<unitFunction IsNull !)
            <unit refcall(<unitFunction)
        endif

        SetColor(<unit @Get("tierColours")[<maxTier])
    loop

:Get
    >var
    GetScriptVar(self "/rts/factories/factory.irpl" <var)

:Set
    >var >val
    SetScriptVar(self "/rts/factories/factory.irpl" <var <val)