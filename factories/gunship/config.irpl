$unit_type:"$/gunship/gunship"
$max_units:2

:Upgrades
    # 0
    "name" "Increased Rocket Firerate"
    "tier" 1
    "costOre" 200
    "costRedon" 0
    "costGreenar" 0
    "costSpectrite" 0
    "attribs" "rocket_fire_delay|-|15"
    "description" "-0.5s rocket fire delay"
    "position" V2(224 250)
    "prerequisite" null
    10 TableN

    # 1
    "name" "Efficient Rocket Engines"
    "tier" 2
    "costOre" 200
    "costRedon" 50
    "costGreenar" 200
    "costSpectrite" 0
    "attribs" "rocket_fire_cost|/|2"
    "description" "-50% rocket energy use"
    "position" V2(176 330)
    "prerequisite" 0
    10 TableN

    # 2
    "name" "Increased Rocket Range"
    "tier" 2
    "costOre" 300
    "costRedon" 150
    "costGreenar" 0
    "costSpectrite" 0
    "attribs" "rocket_fire_range|+|40"
    "description" "+40 Rocket Range"
    "position" V2(272 330)
    "prerequisite" 0
    10 TableN

    # 3
    "name" "Additional Gunship"
    "tier" 3
    "costOre" 500
    "costRedon" 100
    "costGreenar" 0
    "costSpectrite" 0
    "attribs" "max_units|+|1"
    "description" "+1 max Gunships"
    "position" V2(176 410)
    "prerequisite" 1
    10 TableN

    # 4
    "name" "Increased Cannon Firerate"
    "tier" 3
    "costOre" 200
    "costRedon" 300
    "costGreenar" 0
    "costSpectrite" 25
    "attribs" "cannon_fire_delay|-|2"
    "description" "-0.06s cannon fire delay"
    "position" V2(272 410)
    "prerequisite" 2
    10 TableN

    # 5
    "name" "Sulf-Sufficiency"
    "tier" 4
    "costOre" 400
    "costRedon" 0
    "costGreenar" 500
    "costSpectrite" 100
    "attribs" "cannon_fire_cost|=|0 & rocket_fire_cost|=|0"
    "description" "-100% energy use (cannon and rocket)"
    "position" V2(176 490)
    "prerequisite" 3
    10 TableN

    # 6
    "name" "Additional Gunship"
    "tier" 4
    "costOre" 750
    "costRedon" 100
    "costGreenar" 0
    "costSpectrite" 200
    "attribs" "max_units|+|1"
    "description" "+1 max Gunships"
    "position" V2(272 490)
    "prerequisite" 4
    10 TableN

    # 7
    "name" "Gunship Mastery"
    "tier" 5
    "costOre" 1000
    "costRedon" 300
    "costGreenar" 200
    "costSpectrite" 300
    "attribs" "overlap|=|1 & rocket_fire_delay|-|30"
    "description" "ALL Gunships can <color=#FFD166>overlap other units<color=#fff><br>-1s rocket fire delay"
    "position" V2(224 570)
    "prerequisite" 5 6 2 ListN
    10 TableN

    List

:DefaultAttributes
    # @DefaultAttributes >attribs
    # Returns a table of attributes and their default values.

    "max_units" 2 # Required on all factories
    "cannon_fire_delay" 10
    "cannon_fire_cost" 0.1
    "rocket_fire_delay" 100
    "rocket_fire_range" 75
    "rocket_fire_cost" 0.5
    "overlap" 0
    Table

:ApplyAttributes
    # @ApplyAttributes
    # Apply attributes to all units.
    # _DATA = {"units": list, "attribs": table}

    <_DATA{"units"} >units
    <_DATA{"attribs"} >attribs

    # Singular attribs
    if (TableContainsKey(<attribs "max_units"))
        <attribs{"max_units"} @Set("max_units")
    endif
    if (TableContainsKey(<attribs "overlap"))
        GetRegisteredScriptVar("unitcontrol" "shipData") >shipData
        GetRegisteredScriptVar("unitcontrol" "ugpradedShipData") >upgradedShipData

        if (<attribs{"overlap"} 1 ==)
            SetTableElement(<shipData{"$/gunship/gunship"} "scale" V2(1 1))
            SetTableElement(<upgradedShipData{"$/gunship/gunship"} "scale" V2(1 1))
        else
            SetTableElement(<shipData{"$/gunship/gunship"} "scale" V2(16 11))
            SetTableElement(<upgradedShipData{"$/gunship/gunship"} "scale" V2(16 11))
        endif
    endif

    # Per-unit attribs
    GetTableKeys(<attribs) >keys
    <keys 0 do
        <keys[i] >attrib
        <units 0 do
            switch
                case (<attrib "cannon_fire_delay" ==)
                    @SetControllerArgOnAllParts(<units[i] "$/weapon_cannon/weapon_cannon.json" "cannon" "fire_delay" <attribs{<attrib})
                endcase
                case (<attrib "cannon_fire_cost" ==)
                    @SetControllerArgOnAllParts(<units[i] "$/weapon_cannon/weapon_cannon.json" "cannon" "fire_cost" <attribs{<attrib})
                endcase
                case (<attrib "rocket_fire_delay" ==)
                    @SetScriptVarOnAllParts(<units[i] "$/weapon_rocket/weapon_rocket.json" "FIRE_DELAY" <attribs{<attrib})
                endcase
                case (<attrib "rocket_fire_range" ==)
                    @SetScriptVarOnAllParts(<units[i] "$/weapon_rocket/weapon_rocket.json" "FIRE_RANGE" <attribs{<attrib})
                endcase
                case (<attrib "rocket_fire_cost" ==)
                    @SetScriptVarOnAllParts(<units[i] "$/weapon_rocket/weapon_rocket.json" "FIRE_COST" <attribs{<attrib})
                endcase
            endswitch
        loop
    loop

:SetScriptVarOnAllParts
    # @SetScriptVarOnAllParts(<unit <partName <var <value)
    # Helper function for @ApplyAttributes.

    >value >var >partName >unit

    GetChildren(<unit) >parts
    <parts 0 do
        if (GetPartData(<parts[i]{"name"}) <partName ==)
            SetScript1Var(<parts[i] <var <value)
        endif
    loop

:SetControllerArgOnAllParts
    # @SetControllerArgOnAllParts(<unit <partName <controllerName <arg <value)
    # Helper function for @ApplyAttributes.

    >value >arg >controllerName >partName >unit

    GetChildren(<unit) >parts
    <parts 0 do
        if (GetPartData(<parts[i]{"name"}) <partName ==)
            SetControllerArg(<parts[i] <controllerName <arg <value)
        endif
    loop

:PredictDPS
    # @PredictDPS
    # Return a predicted DPS for the given attribs.
    # <_DATA is a table of attribs like @DefaultAttributes.

    1 >numCannons
    1 >numRockets
    7 7 PI * * asint >rocketTiles

    if (TableContainsKey(<_DATA "depth")) <_DATA{"depth"}
    else 2147483647
    endif >depth

    18 >count
    2 <depth min >damage
    <_DATA{"cannon_fire_delay"} GetUpgradeBoost - 30.0 / >rate
    <count <damage * <rate / <numCannons * >cannonDps

    <_DATA{"rocket_fire_delay"} GetUpgradeBoost 6 * - 30.0 / >rate
    40 <depth min >damage
    <damage <rocketTiles * <rate / <numRockets * >rocketDps

    <cannonDps <rocketDps + <_DATA{"max_units"} * @Set("dps")

:Destroyed
    # Undo overlap modifier when destroyed
    "units" @Get("units")
    "overlap" 0 1 TableN
    2 TableN >_DATA
    @ApplyAttributes

# IGNORE BELOW

:Once
    @Upgrades          @Set("upgrades")
    @DefaultAttributes @Set("attribs")
    <unit_type         @Set("unit_type")

    RegisterForMsg("ApplyAttributes"      self 1 - Concat "ApplyAttributes")
    RegisterForMsg("PredictDPS"           self 1 - Concat "PredictDPS")
    RegisterForMsg("SetDefaultAttributes" self 1 - Concat "SetDefaultAttributes")

:SetDefaultAttributes
    @DefaultAttributes @Set("attribs")

:Get
    >var
    GetScriptVar(self "/rts/factories/factory.irpl" <var)

:Set
    >var >val
    SetScriptVar(self "/rts/factories/factory.irpl" <var <val)